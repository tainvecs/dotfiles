# ------------------------------------------------------------------------------
#
# Dotfiles Environment Configuration
#
#
# Version: 0.0.3
# Last Modified: 2025-06-21
#
# ------------------------------------------------------------------------------


# ------------------------------------------------------------------------------
# Ensure Zsh Environment
# ------------------------------------------------------------------------------


# Ensure we're running in zsh
[[ -n "$ZSH_VERSION" ]] || { echo "This script requires zsh" >&2; exit 1; }


# ------------------------------------------------------------------------------
# DOTFILES_ROOT_DIR
# ------------------------------------------------------------------------------


# **Root Directory Determination**
#
# %x expands to script name,
# :A resolves symlinks,
# :h:h:h goes up 3 directories to find the root

# Check if DOTFILES_ROOT_DIR is already set
if [[ -n "$DOTFILES_ROOT_DIR" ]]; then
    :
# Check if the current script path is available
elif [[ -z "${(%):-%x}" ]]; then
    echo "Error: Unable to determine 'dotfiles.env' location" >&2
    return 1
# Set DOTFILES_ROOT_DIR based on script location
else
    export DOTFILES_ROOT_DIR="${${(%):-%x}:A:h:h:h}"
    readonly DOTFILES_ROOT_DIR
fi


# ------------------------------------------------------------------------------
# DOTFILES_DOT_ROOT_DIR, DOTFILES_DOT_ENV_DIR
# ------------------------------------------------------------------------------


# **Dotfiles Root**
[[ -z "$DOTFILES_DOT_ROOT_DIR" ]] && {
    export DOTFILES_DOT_ROOT_DIR="$DOTFILES_ROOT_DIR/.dotfiles"
    readonly DOTFILES_DOT_ROOT_DIR
}

# **Environment Variable**
[[ -z "$DOTFILES_DOT_ENV_DIR" ]] && {
    export DOTFILES_DOT_ENV_DIR="$DOTFILES_DOT_ROOT_DIR/env"
    readonly DOTFILES_DOT_ENV_DIR
}


# ------------------------------------------------------------------------------
# Load Environment Files
# ------------------------------------------------------------------------------


# load all .env files in DOTFILES_DOT_ENV_DIR
if [[ -d "$DOTFILES_DOT_ENV_DIR" ]]; then

    _cur_script="${${(%):-%x}:A}"

    for _env_file in $DOTFILES_DOT_ENV_DIR/*.env(.N); do
        if [[ "${_env_file:A}" != "$_cur_script" ]]; then
            source "$_env_file" || echo "Error: Failed to source '$_env_file'" >&2
        fi
    done
else
    echo "Warn: No environment directory found at '$DOTFILES_DOT_ENV_DIR'." >&2
fi

# load all .env files in DOTFILES_USER_ENV_DIR
if [[ -d "$DOTFILES_USER_ENV_DIR" ]]; then
    for _env_file in $DOTFILES_USER_ENV_DIR/*.env(.N); do
        source "$_env_file" || echo "Error: Failed to source '$_env_file'" >&2
    done
else
    echo "Info: No environment directory found at '$DOTFILES_USER_ENV_DIR'." >&2
fi
